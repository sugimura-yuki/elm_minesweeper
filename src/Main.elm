module Main exposing (main)

import Browser
import Dict
import Html
import Html.Attributes as Attrs
import Html.Events as Events
import Json.Decode
import Minesweeper.Message exposing (Msg(..))
import Minesweeper.Model exposing (CellStatus(..), Model, Pos, Setting)
import Random
import Set


main : Program () Model Msg
main =
    Browser.document
        { init = init
        , view = view
        , update = update
        , subscriptions = \_ -> Sub.none
        }


{-| ãƒ©ãƒ³ãƒ€ãƒ ä½ç½®ã«çˆ†å¼¾ã‚’é…ç½®
-}
randomBomb : Setting -> Cmd Msg
randomBomb setting =
    let
        randomPos =
            Random.map2
                (\i j -> ( i, j ))
                (Random.int 1 setting.rows)
                (Random.int 1 setting.cols)
    in
    Random.generate SetBomb randomPos


init : flags -> ( Model, Cmd Msg )
init _ =
    reset
        { rows = 20
        , cols = 20
        , bombs = 50
        }


reset : Setting -> ( Model, Cmd Msg )
reset setting =
    let
        --| ãƒ¢ãƒ‡ãƒ«ã®åˆæœŸçŠ¶æ…‹
        initModel =
            { bombs = Set.empty
            , cells = Dict.empty
            , setting = setting
            , settingForm = setting
            }

        --| åˆæœŸå®Ÿè¡Œã‚³ãƒžãƒ³ãƒ‰
        initCmd =
            randomBomb initModel.setting
    in
    ( initModel, initCmd )


view : Model -> Browser.Document Msg
view model =
    let
        cell ( r, c ) =
            let
                pos =
                    ( r, c )

                currentCell =
                    Dict.get pos model.cells

                cellText =
                    case currentCell of
                        Just Flag ->
                            "ðŸ"

                        Just Bomb ->
                            "ðŸ’£"

                        Just (Safe 0) ->
                            ""

                        Just (Safe n) ->
                            String.fromInt n

                        _ ->
                            ""
            in
            Html.td
                [ Attrs.style "width" "30px"
                , Attrs.style "height" "30px"
                , Attrs.style "border" "1px solid black"
                , Attrs.style "user-select" "none"
                , Attrs.style "text-align" "center"
                , Attrs.style "cursor" <|
                    if currentCell == Nothing then
                        "pointer"

                    else
                        "auto"
                , Attrs.style "background-color" <|
                    if currentCell == Nothing then
                        "#aaa"

                    else
                        "#fff"
                , Events.preventDefaultOn "contextmenu" (Json.Decode.succeed ( SetFlag pos, True ))
                , Events.onClick (Open pos)
                ]
                [ Html.text cellText ]

        table =
            List.range 1 model.setting.rows
                |> List.map
                    (\r ->
                        List.range 1 model.setting.cols
                            |> List.map (\c -> cell ( r, c ))
                            |> Html.tr []
                    )
                |> Html.table
                    [ Attrs.style "border-collapse" "collapse"
                    ]

        bombText =
            String.fromInt (model.cells |> Dict.filter (\_ a -> a == Flag) |> Dict.size) ++ " / " ++ String.fromInt model.setting.bombs

        description =
            Html.dl []
                [ Html.dt [] [ Html.text "å·¦ã‚¯ãƒªãƒƒã‚¯" ]
                , Html.dd [] [ Html.text "ã‚»ãƒ«ã‚’ã²ã‚‰ã" ]
                , Html.dt [] [ Html.text "å³ã‚¯ãƒªãƒƒã‚¯" ]
                , Html.dd [] [ Html.text "æ——ã‚’ç«‹ã¦ã‚‹" ]
                , Html.dt [] [ Html.text "ãƒ•ãƒ©ã‚°ç«‹ã¦ãŸæ•° / çˆ†å¼¾æ•°" ]
                , Html.dd [] [ Html.text bombText ]
                ]

        form =
            let
                settingForm =
                    model.settingForm

                makeInputForm labelText value updateSetting =
                    Html.label
                        [ Attrs.style "display" "block"
                        ]
                        [ Html.dl []
                            [ Html.dt [] [ Html.text labelText ]
                            , Html.dd []
                                [ Html.input
                                    [ Attrs.type_ "number"
                                    , Attrs.value (value |> String.fromInt)
                                    , Events.onInput
                                        (\val ->
                                            case String.toInt val of
                                                Nothing ->
                                                    InputForm settingForm

                                                Just n ->
                                                    InputForm (updateSetting n)
                                        )
                                    ]
                                    []
                                ]
                            ]
                        ]
            in
            Html.div []
                [ makeInputForm "ç¸¦ã®ã‚µã‚¤ã‚º" settingForm.rows (\n -> { settingForm | rows = n })
                , makeInputForm "æ¨ªã®ã‚µã‚¤ã‚º" settingForm.cols (\n -> { settingForm | cols = n })
                , makeInputForm "çˆ†å¼¾ã®æ•°" settingForm.bombs (\n -> { settingForm | bombs = n })
                , Html.button [ Events.onClick Reset ] [ Html.text "å†ä½œæˆ" ]
                ]
    in
    { title = "ãƒžã‚¤ãƒ³ã‚¹ã‚¤ãƒ¼ãƒ‘ãƒ¼"
    , body =
        [ Html.main_ []
            [ Html.div
                [ Attrs.style "display" "flex"
                , Attrs.style "flex-wrap" "wrap"
                , Attrs.style "gap" "10px"
                ]
                [ table
                , Html.div []
                    [ description
                    , form
                    ]
                ]
            ]
        ]
    }


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        -- æ——ã‚’ç«‹ã¦ã‚‹
        SetFlag pos ->
            let
                cells =
                    model.cells
                        |> Dict.update pos
                            (\v ->
                                case v of
                                    Nothing ->
                                        Just Flag

                                    Just Flag ->
                                        Nothing

                                    _ ->
                                        v
                            )
            in
            ( { model | cells = cells }, Cmd.none )

        -- ã‚»ãƒ«ã‚’é–‹ã
        Open pos ->
            ( openCell pos model, Cmd.none )

        -- çˆ†å¼¾ã‚’ã‚»ãƒƒãƒˆ
        SetBomb pos ->
            let
                bombs =
                    Set.insert pos model.bombs

                cmd =
                    if Set.size bombs < model.setting.bombs then
                        randomBomb model.setting

                    else
                        Cmd.none
            in
            ( { model | bombs = bombs }, cmd )

        InputForm form ->
            ( { model | settingForm = form }, Cmd.none )

        Reset ->
            reset model.settingForm


openCell : Pos -> Model -> Model
openCell pos model =
    let
        ( r, c ) =
            pos

        -- å‘¨å›²ã®ã‚»ãƒ«
        arround =
            [ ( r, c - 1 )
            , ( r, c + 1 )
            , ( r - 1, c - 1 )
            , ( r - 1, c )
            , ( r - 1, c + 1 )
            , ( r + 1, c - 1 )
            , ( r + 1, c )
            , ( r + 1, c + 1 )
            ]
                |> List.filter (\( a, b ) -> 0 < a && a <= model.setting.rows && 0 < b && b <= model.setting.cols)

        -- å‘¨å›²ã®çˆ†å¼¾æ•°
        countCell =
            arround
                |> List.filter (\p -> Set.member p model.bombs)
                |> List.length

        -- é–‹ã„ãŸå¾Œã®ã‚»ãƒ«çŠ¶æ…‹
        cellStatus =
            if Set.member pos model.bombs then
                Bomb

            else
                Safe countCell

        updatedModel =
            { model | cells = Dict.insert pos cellStatus model.cells }
    in
    case Dict.get pos model.cells of
        -- ã¾ã ç©ºã„ã¦ãªã„å ´åˆ
        Nothing ->
            -- ã¾ã‚ã‚Šã®çˆ†å¼¾ãŒï¼å€‹ãªã‚‰å‘¨ã‚Šã‚‚ã‚ã‘ã‚‹
            if cellStatus == Safe 0 then
                arround
                    |> List.foldl
                        openCell
                        updatedModel

            else
                updatedModel

        -- ã‚‚ã†ç©ºã„ã¦ã‚‹ãªã‚‰ä½•ã‚‚ã—ãªã„
        _ ->
            model
