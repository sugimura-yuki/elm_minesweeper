module Main exposing (main)

import Browser
import Dict
import Html
import Html.Attributes as Attrs
import Html.Events as Events
import Json.Decode
import Random
import Set


main : Program () Model Msg
main =
    Browser.document
        { init = init
        , view = view
        , update = update
        , subscriptions = \_ -> Sub.none
        }


{-| è¨­å®š
-}
setting : { rows : Int, cols : Int, bombs : Int }
setting =
    { rows = 20
    , cols = 20
    , bombs = 50
    }


{-| ãƒ©ãƒ³ãƒ€ãƒ ä½ç½®ã«çˆ†å¼¾ã‚’é…ç½®
-}
addRandomBomb : Cmd Msg
addRandomBomb =
    Random.map2
        (\i j -> ( i, j ))
        (Random.int 1 setting.rows)
        (Random.int 1 setting.cols)
        |> Random.generate SetBomb


{-| ã‚»ãƒ«ã®çŠ¶æ…‹

  - Safe = çˆ†å¼¾ãªã—(ã¾ã‚ã‚Šã®çˆ†å¼¾ã®æ•°)
  - Bomb = çˆ†å¼¾
  - Flag = æ——ã‚’ç«‹ã¦ã¦ã„ã‚‹

-}
type CellStatus
    = Safe Int
    | Bomb
    | Flag


{-| çˆ†å¼¾ä½ç½®
-}
type alias Bombs =
    Set.Set Pos


{-| ä½ç½®
-}
type alias Pos =
    ( Int, Int )


{-| é–‹ãæ¸ˆã¿ã®ã‚»ãƒ«çŠ¶æ…‹
-}
type alias Cells =
    Dict.Dict Pos CellStatus


{-| ãƒ¢ãƒ‡ãƒ«
-}
type alias Model =
    { bombs : Bombs
    , cells : Cells
    }


{-| ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸

  - Open = è©²å½“ä½ç½®ã®ã‚»ãƒ«ã‚’é–‹ã
  - SetFlag = è©²å½“ä½ç½®ã®ã‚»ãƒ«ã«æ——ã‚’ç«‹ã¦ã‚‹
  - SetBomb = è©²å½“ä½ç½®ã«çˆ†å¼¾ã‚’ã‚»ãƒƒãƒˆã™ã‚‹

-}
type Msg
    = Open Pos
    | SetFlag Pos
    | SetBomb Pos


init : flags -> ( Model, Cmd Msg )
init _ =
    let
        --| ãƒ¢ãƒ‡ãƒ«ã®åˆæœŸçŠ¶æ…‹
        initModel =
            { bombs = Set.empty, cells = Dict.empty }

        --| åˆæœŸå®Ÿè¡Œã‚³ãƒžãƒ³ãƒ‰
        initCmd =
            addRandomBomb
    in
    ( initModel, initCmd )


view : Model -> Browser.Document Msg
view model =
    let
        table =
            List.range 1 setting.rows
                |> List.map
                    (\r ->
                        List.range 1 setting.cols
                            |> List.map
                                (\c ->
                                    let
                                        pos =
                                            ( r, c )

                                        currentCell =
                                            Dict.get pos model.cells

                                        cellText =
                                            Html.text <|
                                                case currentCell of
                                                    Just Flag ->
                                                        "ðŸ"

                                                    Just Bomb ->
                                                        "ðŸ’£"

                                                    Just (Safe 0) ->
                                                        ""

                                                    Just (Safe n) ->
                                                        String.fromInt n

                                                    _ ->
                                                        ""
                                    in
                                    Html.td
                                        [ Attrs.style "width" "30px"
                                        , Attrs.style "height" "30px"
                                        , Attrs.style "border" "1px solid black"
                                        , Attrs.style "user-select" "none"
                                        , Attrs.style "text-align" "center"
                                        , Attrs.style "cursor" <|
                                            if currentCell == Nothing then
                                                "pointer"

                                            else
                                                "auto"
                                        , Attrs.style "background-color" <|
                                            if currentCell == Nothing then
                                                "#aaa"

                                            else
                                                "#fff"
                                        , Events.preventDefaultOn "contextmenu" (Json.Decode.succeed ( SetFlag pos, True ))
                                        , Events.onClick (Open pos)
                                        ]
                                        [ cellText ]
                                )
                            |> Html.tr []
                    )
                |> Html.table
                    [ Attrs.style "border-collapse" "collapse"
                    ]

        bombText =
            String.fromInt (model.cells |> Dict.filter (\_ a -> a == Flag) |> Dict.size) ++ " / " ++ String.fromInt setting.bombs

        description =
            Html.dl []
                [ Html.dt [] [ Html.text "å·¦ã‚¯ãƒªãƒƒã‚¯" ]
                , Html.dd [] [ Html.text "ã‚»ãƒ«ã‚’ã²ã‚‰ã" ]
                , Html.dt [] [ Html.text "å³ã‚¯ãƒªãƒƒã‚¯" ]
                , Html.dd [] [ Html.text "æ——ã‚’ç«‹ã¦ã‚‹" ]
                , Html.dt [] [ Html.text "ãƒ•ãƒ©ã‚°ç«‹ã¦ãŸæ•° / çˆ†å¼¾æ•°" ]
                , Html.dd [] [ Html.text bombText ]
                ]
    in
    { title = "ãƒžã‚¤ãƒ³ã‚¹ã‚¤ãƒ¼ãƒ‘ãƒ¼"
    , body =
        [ Html.main_ []
            [ Html.div
                [ Attrs.style "display" "flex"
                , Attrs.style "flex-wrap" "wrap"
                , Attrs.style "gap" "10px"
                ]
                [ table
                , description
                ]
            ]
        ]
    }


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        -- æ——ã‚’ç«‹ã¦ã‚‹
        SetFlag pos ->
            let
                cells =
                    model.cells
                        |> Dict.update pos
                            (\v ->
                                case v of
                                    Nothing ->
                                        Just Flag

                                    Just Flag ->
                                        Nothing

                                    _ ->
                                        v
                            )
            in
            ( { model | cells = cells }, Cmd.none )

        -- ã‚»ãƒ«ã‚’é–‹ã
        Open pos ->
            ( { model | cells = updateCells model.bombs pos model.cells }, Cmd.none )

        -- çˆ†å¼¾ã‚’ã‚»ãƒƒãƒˆ
        SetBomb pos ->
            let
                bombs =
                    Set.insert pos model.bombs

                cmd =
                    if Set.size bombs >= setting.bombs then
                        -- è¦å®šæ•°ã«åˆ°é”
                        Cmd.none

                    else
                        -- ã•ã‚‰ã«è¿½åŠ 
                        addRandomBomb
            in
            ( { model | bombs = bombs }, cmd )


updateCells : Bombs -> Pos -> Cells -> Cells
updateCells bombs pos cells =
    let
        ( r, c ) =
            pos

        -- å‘¨å›²ã®ã‚»ãƒ«
        arround =
            [ ( r, c - 1 )
            , ( r, c + 1 )
            , ( r - 1, c - 1 )
            , ( r - 1, c )
            , ( r - 1, c + 1 )
            , ( r + 1, c - 1 )
            , ( r + 1, c )
            , ( r + 1, c + 1 )
            ]
                |> List.filter (\( a, b ) -> 0 < a && a <= setting.rows && 0 < b && b <= setting.cols)

        -- å‘¨å›²ã®çˆ†å¼¾æ•°
        countCell =
            arround
                |> List.filter (\p -> Set.member p bombs)
                |> List.length

        -- é–‹ã„ãŸå¾Œã®ã‚»ãƒ«çŠ¶æ…‹
        openCell =
            if Set.member pos bombs then
                Bomb

            else
                Safe countCell
    in
    case Dict.get pos cells of
        -- ã¾ã ç©ºã„ã¦ãªã„å ´åˆ
        Nothing ->
            -- ã¾ã‚ã‚Šã®çˆ†å¼¾ãŒï¼å€‹ãªã‚‰å‘¨ã‚Šã‚‚ã‚ã‘ã‚‹
            if openCell == Safe 0 then
                arround
                    |> List.foldl
                        (\a b -> updateCells bombs a b)
                        (Dict.insert pos openCell cells)

            else
                Dict.insert pos openCell cells

        -- ã‚‚ã†ç©ºã„ã¦ã‚‹ãªã‚‰ä½•ã‚‚ã—ãªã„
        _ ->
            cells
